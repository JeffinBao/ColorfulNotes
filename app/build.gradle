apply plugin: 'com.android.application'

buildscript {
    repositories {
        mavenCentral()
    }
}

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.jeffinbao.colorfulnotes"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 110
        versionName "1.1.0"

        //umeng default channel
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }

    signingConfigs {
        debug {
            storeFile file(System.getProperty("user.home") + "/.android/debug.keystore")
        }

        release {
            storeFile file(ANDROID_KEY_STORE_FILE_PATH)
            storePassword ANDROID_KEY_STORE_PASSWORD
            keyAlias ANDROID_KEY_ALIAS
            keyPassword ANDROID_KEY_PASSWORD
        }
    }
    buildTypes {
        debug {
            buildConfigField("boolean", "LOG_DEBUG", "true")
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField("boolean", "LOG_DEBUG", "false")
            zipAlignEnabled true
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = "cn_${defaultConfig.versionName}_${variant.flavorName}.apk"
                }
            }
        }
    }

    flavorDimensions "channel"

    productFlavors {
        smartisan {
            dimension "channel"
        }
        xiaomi {
            dimension "channel"
        }
        huawei {
            dimension "channel"
        }
        meizu {
            dimension "channel"
        }
        googleplay {
            dimension "channel"
        }
        alibaba {
            dimension "channel"
        }
        tencent {
            dimension "channel"
        }
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'de.greenrobot:eventbus:3.0.0-beta1'
    implementation 'com.evernote:android-sdk:2.0.0-RC3'
    implementation 'com.umeng.umsdk:analytics:7.5.4'
    implementation 'com.umeng.umsdk:common:1.5.4'
}
